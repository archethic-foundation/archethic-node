defmodule Archethic.Utils.Regression.Benchmark.LegacySmartContractTrigger do
  @moduledoc false

  require Logger

  alias Archethic.Crypto

  alias Archethic.Utils.Regression.Api
  alias Archethic.Utils.Regression.Playbook.SmartContract
  alias Archethic.Utils.Regression.Benchmark.SeedHolder
  alias Archethic.Utils.Regression.Benchmark.SmartContractHelper
  alias Archethic.Utils.Regression.Benchmark
  alias Archethic.Utils.WebSocket.Client, as: WSClient

  alias Archethic.TransactionChain.TransactionData

  @behaviour Benchmark
  def plan([host | _nodes], _opts) do
    port = Application.get_env(:archethic, ArchethicWeb.Endpoint)[:http][:port]

    endpoint = %Api{host: host, port: port, protocol: :http}

    WSClient.start_link(host: host, port: port)
    Crypto.Ed25519.LibSodiumPort.start_link()
    Logger.info("Starting Benchmark: Transactions Per Seconds at host #{host} and port #{port}")

    storage_nonce_pubkey = Api.get_storage_nonce_public_key(endpoint)

    {:ok, pid} =
      SeedHolder.start_link(seeds: Enum.map(0..200, fn _ -> :crypto.strong_rand_bytes(32) end))

    amount = 10

    contract_seed = :crypto.strong_rand_bytes(32)

    genesis_address =
      Crypto.derive_keypair(contract_seed, 0) |> elem(0) |> Crypto.derive_address()

    Api.send_funds_to_seeds(
      [contract_seed | SeedHolder.get_seeds(pid)]
      |> Enum.map(fn seed -> {seed, amount} end)
      |> Enum.into(%{}),
      endpoint
    )

    contract_address =
      SmartContract.deploy(
        contract_seed,
        %TransactionData{
          content: "0",
          code: contract_code()
        },
        storage_nonce_pubkey,
        endpoint
      )

    {
      %{
        "Legacy SC trigger" => fn ->
          {trigger_seed, _} = SeedHolder.pop_seed(pid)

          {:ok, trigger_address} =
            SmartContract.trigger(trigger_seed, contract_address, endpoint,
              await_timeout: 60_000,
              version: 3
            )

          SmartContractHelper.await_no_more_calls(genesis_address, trigger_address, endpoint)

        end
      },
      [parallel: 4]
    }
  end

  defp contract_code() do
    ~s"""
    @version 1

    # GENERATED BY PLAYBOOK

    condition transaction: []
    actions triggered_by: transaction do
        count = String.to_number(contract.content) + 1
        Contract.set_content count
    end
    """
  end
end
